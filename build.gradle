apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'sonar-runner'

sourceCompatibility = 1.7
project.version = '5.3.5'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'http://ci.snippingtoolpluspl.us/repository/'
    }
}

jar {

	from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    
    manifest {
        attributes ('Main-Class': 'com.shaneisrael.st.Main',
                    'Application-Version': project.version,
                    'Author': 'http://snippingtoolpluspl.us',
                    'Built-By': System.getProperty("user.name"),
                    'Built-On': new Date().toString())
    }
    
    archiveName = project.name + '-' + project.version + '.jar'
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'commons-codec:commons-codec:1.8'
    compile 'com.miglayout:miglayout:3.7.4'
    compile 'com.google.code.gson:gson:2.2.4'

    compile 'us.snippingtoolpluspl:stnotifications:1.0.0'
}

eclipse {
	project {
		name = 'snippingtoolplusplus'
	}
}

test {
    systemProperties 'property': 'value'
}

jacoco {
    toolVersion = '0.7.0.201403182114'
}

jacocoTestReport {
    additionalSourceDirs = files("src/main/java")
    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/xmlunittest"
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

//run gradlew wrapper to update the project's gradle version
task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://sonar.snippingtoolpluspl.us"
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "$System.env.SONAR_PASSWORD"
    }
}
